#version 460 core
#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_GOOGLE_include_directive : require

#include "ray_common.glsl"
#include "ray_kusochki.glsl"

layout (set = 0, binding = 6) uniform sampler2D textures[MAX_TEXTURES];

layout (push_constant) uniform PC_ {
	PushConstants push_constants;
};

layout(location = PAYLOAD_LOCATION_ADDITIVE) rayPayloadInEXT RayPayloadAdditive payload_additive;

hitAttributeEXT vec2 bary;

void main() {
    const int instance_kusochki_offset = gl_InstanceCustomIndexEXT;
    const int kusok_index = instance_kusochki_offset + gl_GeometryIndexEXT;
    const uint first_index_offset = getKusok(kusok_index).index_offset + gl_PrimitiveID * 3;

    const uint vi1 = uint(getIndex(first_index_offset+0)) + getKusok(kusok_index).vertex_offset;
    const uint vi2 = uint(getIndex(first_index_offset+1)) + getKusok(kusok_index).vertex_offset;
    const uint vi3 = uint(getIndex(first_index_offset+2)) + getKusok(kusok_index).vertex_offset;

    const vec2 texture_uv = GET_VERTEX(vi1).gl_tc * (1. - bary.x - bary.y) + GET_VERTEX(vi2).gl_tc * bary.x + GET_VERTEX(vi3).gl_tc * bary.y + push_constants.time * getKusok(kusok_index).uv_speed;
    // TODO mips
    const uint tex_index = getKusok(kusok_index).tex_base_color;
    const vec4 texture_color = texture(textures[nonuniformEXT(tex_index)], texture_uv);
	const vec3 color = texture_color.rgb * getKusok(kusok_index).color.rgb * texture_color.a * getKusok(kusok_index).color.a;

	const float overshoot = gl_HitTEXT - payload_additive.ray_distance;

	payload_additive.color += color * smoothstep(additive_soft_overshoot, 0., overshoot);

	ignoreIntersectionEXT;
}
